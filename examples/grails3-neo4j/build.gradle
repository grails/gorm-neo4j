group "examples"
configurations.all { Configuration c ->
    c.exclude(group:'org.grails',module:'grails-datastore-simple')
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "javax.servlet:javax.servlet-api:$servletApiVersion"
    implementation "org.grails:grails-core:$grailsVersion"
    implementation "org.grails:grails-dependencies:$grailsVersion"
    implementation "org.grails:grails-web-boot:$grailsVersion"
    implementation "org.neo4j.driver:neo4j-java-driver:$neo4jDriverVersion"

    // in a real app this would be org.grails.plugins:neo4j
    implementation project(":grails-plugin")

    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:$assetPipelineVersion"
    runtimeOnly "org.grails.plugins:scaffolding:$scaffoldingVersion"
    runtimeOnly "org.grails.plugins:fields:$fieldsVersion"

    testImplementation ("org.grails:grails-testing-support:$testingSupportVersion") {
        exclude group: "org.spockframework", module: "spock-core"
    }
    testImplementation ("org.grails:grails-gorm-testing-support:$testingSupportVersion") {
        exclude group: "org.spockframework", module: "spock-core"
    }
    testImplementation ("org.grails:grails-web-testing-support:$testingSupportVersion") {
        exclude group: "org.spockframework", module: "spock-core"
    }

    testImplementation("org.grails.plugins:geb") {
        exclude group: 'org.gebish', module: 'geb-spock'
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testImplementation ("org.gebish:geb-spock:$gebVersion")

    testRuntimeOnly "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-safari-driver:$seleniumSafariDriverVersion"

    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-support:$seleniumVersion"

    testRuntimeOnly "org.neo4j.test:neo4j-harness:$neo4jVersion"

    profile "org.grails.profiles:web"
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    if (!System.getenv().containsKey('CI')) {
        systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
        systemProperty 'webdriver.gecko.driver', System.getProperty('webdriver.gecko.driver')
    } else {
        systemProperty 'webdriver.chrome.driver', "${System.getenv('CHROMEWEBDRIVER')}/chromedriver"
        systemProperty 'webdriver.gecko.driver', "${System.getenv('GECKOWEBDRIVER')}/geckodriver"
    }
}

webdriverBinaries {
    if (!System.getenv().containsKey('CI')) {
        chromedriver "$chromeDriverVersion"
        geckodriver "$geckodriverVersion"
    }
}