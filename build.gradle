import groovyx.net.http.RESTClient

import static groovyx.net.http.ContentType.JSON

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        // classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath "io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1"
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath "gradle.plugin.com.energizedwork.webdriver-binaries:webdriver-binaries-gradle-plugin:$webdriverBinariesVersion"
    }
}

group "org.grails"
version "7.0.0.M1"
//version "6.1.5.RELEASE"
println "GORM VERSION = ${project.gormVersion}"

ext {
    isTravisBuild = System.getenv().get("TRAVIS") == 'true'
    hibernatePluginVersion = "6.1.8"
    
    isCiBuild = project.hasProperty("isCiBuild")
    isBuildSnapshot = version.endsWith("BUILD-SNAPSHOT")
    nexusUsername = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
    nexusPassword = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
}

subprojects { project ->

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }

    boolean usesGeb = ["grails3-neo4j", "grails3-neo4j-hibernate"].any { projectName -> project.name.contains(projectName) }

    if(project.name.startsWith("examples-grails3")) {
        apply plugin:"org.grails.grails-web"
        apply plugin:"org.grails.grails-gsp"

        if (usesGeb) {
            apply plugin:"com.energizedwork.webdriver-binaries"
        }

        tasks.withType(Test) {
            testLogging {
                showStandardStreams = true
                exceptionFormat = 'full'
            }
        }
        configurations.all {
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                if(details.requested.group == 'org.codehaus.groovy' && details.requested.name.startsWith('groovy')) {
                    details.useVersion(groovyVersion)   
                }
                if(details.requested.group == 'org.springframework') {
                    details.useVersion(springVersion)
                }
            }
        } 

        return
    }

    boolean isPluginProject = project.name.endsWith("-plugin") && project.name.startsWith("grails")
    boolean isGrails3PluginProject = project.name.endsWith("-plugin")
    boolean isExample = project.name.startsWith("examples-")

    if (isExample) {
        apply plugin:'groovy'
        return
    }

    ext {
        projectInfo = new PublishingConvention(project)
        pomInfo = {
            delegate.name projectInfo.projectName
            delegate.description projectInfo.projectDescription
            delegate.url projectInfo.projectURL

            delegate.licenses {
                delegate.license {
                    delegate.name 'The Apache Software License, Version 2.0'
                    delegate.url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    delegate.distribution 'repo'
                }
            }

            delegate.scm {
                delegate.url projectInfo.projectVcsUrl
                delegate.connection projectInfo.projectVcsUrl
                delegate.developerConnection projectInfo.projectVcsUrl
            }

            delegate.developers {
                delegate.developer {
                    delegate.id 'graemerocher'
                    delegate.name 'Graeme Rocher'
                }
                delegate.developer {
                    delegate.id 'jeffscottbrown'
                    delegate.name 'Jeff Brown'
                }
                delegate.developer {
                    delegate.id 'burtbeckwith'
                    delegate.name 'Burt Beckwith'
                }
            }
        }         
    }
       
    if(isPluginProject) {        
        group "org.grails.plugins"
        version( rootProject.version - '.RELEASE' )
    }
    else {
        group "org.grails"
        version rootProject.version
    }

    configurations {
        documentation
        all {
            resolutionStrategy {
                cacheDynamicVersionsFor 0, 'hours'
                cacheChangingModulesFor 0, 'hours'
            }
        }        
    }

    if(isPluginProject) {
        group "org.grails.plugins"
    }
    else {
        group "org.grails"        
    }

    if(project.name == 'docs') {
        return
    }       

    if(isGrails3PluginProject) {
        apply plugin: "org.grails.grails-plugin"       
    }
    else {
        apply plugin:"groovy"
        apply plugin: 'com.bmuschko.nexus'
        modifyPom {
            delegate.project pomInfo
        }
    }

    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: "io.github.groovylang.groovydoc"

    groovydoc.jvmArgs "-Xmx512M"
    groovydoc.classpath += configurations.documentation

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    dependencies {
        documentation "org.fusesource.jansi:jansi:1.11"
        documentation 'info.picocli:picocli:3.8.2'
        documentation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
        testCompile "org.spockframework:spock-core:$spockVersion"
    }

    publishing {

        repositories {
            maven {
                credentials {
                    def u = System.getenv("ARTIFACTORY_USERNAME") ?: project.hasProperty("artifactoryPublishUsername") ? project.artifactoryPublishUsername : ''
                    def p = System.getenv("ARTIFACTORY_PASSWORD") ?: project.hasProperty("artifactoryPublishPassword") ? project.artifactoryPublishPassword : ''
                    username = u
                    password = p
                }
                if(isGrails3PluginProject) {
                    if(isBuildSnapshot) {
                        url  "https://repo.grails.org/grails/plugins3-snapshots-local"
                    }
                    else {
                        url  "https://repo.grails.org/grails/plugins3-releases-local"
                    }
                }
                else {
                    if(isBuildSnapshot) {
                        url  "https://repo.grails.org/grails/libs-snapshots-local"
                    }
                    else {
                        url  "https://repo.grails.org/grails/libs-releases-local"
                    }
                }
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId projectInfo.projectArtifactId
                from components.java

                afterEvaluate {
                    artifact source: sourcesJar, classifier: "sources"
                    artifact source: javadocJar, classifier: "javadoc"


                    if (isGrails3PluginProject) {
                        artifact source:"${sourceSets.main.groovy.outputDir}/META-INF/grails-plugin.xml",
                                classifier:"plugin",
                                extension:'xml'
                    }

                }

                pom.withXml {
                    def xml = asNode()
                    def dependency = xml.dependencies.find { dep -> dep.artifactId == 'slf4j-simple' }
                    dependency?.optional = true
                    xml.children().last() + pomInfo

                }
            }
        }
    }

    bintray {

        def u = System.getenv("BINTRAY_USER") ?: project.hasProperty("bintrayUser") ? project.bintrayUser : ''
        def p = System.getenv("BINTRAY_KEY") ?: project.hasProperty("bintrayKey") ? project.bintrayKey : ''

        user = u
        key = p
        publications = ['maven']
        publish = true
        dryRun = false
        pkg {
            if(isGrails3PluginProject) {
                repo = 'plugins'
            }
            else {
                repo = 'grails-core'
            }
            userOrg = 'grails'
            name = projectInfo.projectArtifactId
            desc = projectInfo.projectDescription
            websiteUrl = projectInfo.projectURL
            issueTrackerUrl = projectInfo.projectIssueTrackerUrl
            vcsUrl = projectInfo.projectVcsUrl
            licenses = ['Apache-2.0']
            publicDownloadNumbers = true
            version {
                name = project.version
                gpg {
                    sign = true
                    passphrase = System.getenv("SIGNING_PASSPHRASE") ?: project.hasProperty("signingPassphrase") ? project.signingPassphrase : ''
                }
                mavenCentralSync {
                    sync = false
                    def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
                    def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
                    user = ossUser
                    password = ossPass
                }

            }
        }
    }

    if (!isGrails3PluginProject) {
        task synchronizeWithMavenCentral() {
            description = "Syncs with Maven Central/Sonatype"
            doLast {
                println """
Synchronizing with Maven central. This may take a few minutes ...
If this fails, log on to http://oss.sonatype.org/ using the centralUser credentials
and progress through process manually -> Close -> Publish ... under staging repositories
"""
                try {
                    try {
                        def mavenClent = new RESTClient("https://repo.maven.apache.org/maven2/")
                        mavenClent.get(path:"${project.group.replace('.','/')}/$project.name/$projectVersion/$project.name-${projectVersion}.jar")
                    } catch(groovyx.net.http.HttpResponseException e) {
                        // doesn't exist.. try sync
                        def client = new RESTClient('https://api.bintray.com/maven_central_sync/grails/grails-core/')
                        client.headers['Authorization'] = 'Basic ' + "$bintrayUser:$bintrayKey".getBytes('iso-8859-1').encodeBase64()
                        def body = /{}/
                        def resp = client.post(
                                path: "$bintray.pkg.name/versions/$projectVersion",
                                body: body,
                                requestContentType: JSON
                        )
                        assert resp.status == 200
                        println resp.data.status
                        println resp.data.messages.join('\n')
                    }
                } catch (groovyx.net.http.HttpResponseException e) {
                    println "Error: $e"
                    println "Message: $e.message"
                    println "Body: $e.response.data"
                    throw e
                }
            }
        }
    }
}

class PublishingConvention {
    Project project

    String projectArtifactId
    String projectName = 'GORM for Neo4j'
    String projectDescription = 'Provides a GORM Object Mapping implementation for the Neo4j Graph Database'
    String projectURL = 'https://gorm.grails.org/latest/neo4j'
    String projectIssueTrackerUrl = 'https://github.com/grails/gorm-neo4j/issues'
    String projectVcsUrl = 'https://github.com/grails/gorm-neo4j'

    PublishingConvention(Project project) {
        this.project = project

        def name = project.name
        if(name.startsWith('grails') && name.endsWith('-plugin')) {
            name = 'neo4j'
        }
        projectArtifactId = name
    }
}
